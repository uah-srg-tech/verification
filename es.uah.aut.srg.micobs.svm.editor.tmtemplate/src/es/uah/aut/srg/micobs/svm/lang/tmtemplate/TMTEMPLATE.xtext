grammar es.uah.aut.srg.micobs.svm.lang.tmtemplate.TMTEMPLATE with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/svm/tmtemplate" 
import "http://srg.aut.uah.es/tmtcif/tm" as tm
import "http://srg.aut.uah.es/tmtcif/fieldvalue" as fieldvalue
import "http://srg.aut.uah.es/tmtcif/enum" as enum_
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

VTMTemplate returns VTMTemplate:
	'<TMTemplate'
		'name=' name=STRING
		'tmOutput=' tmOutput=[tm::TMTCIFTM|STRING]
	(('>' ('<Fields>' (fields+=VTMTemplateField)+ '</Fields>')? '</TMTemplate>') |
	('/>'))
;

VTMTemplateField returns VTMTemplateField:
	'<Field'
		'fieldRef=' fieldRef=[tm::TMTCIFTMField|STRING] '>'
		value=TMTCIFFieldValue 
	'</Field>';
	
TMTCIFFieldValue returns fieldvalue::TMTCIFFieldValue:
	TMTCIFFieldValueRaw | TMTCIFFieldValueFile | TMTCIFFieldValueEnum;

TMTCIFFieldValueRaw returns fieldvalue::TMTCIFFieldValueRaw:
	'<FieldValueRaw' 'value=' value=(UINT_STRING | HEX_STRING) '/>';

TMTCIFFieldValueFile returns fieldvalue::TMTCIFFieldValueFile:
	'<FieldValueFile' 'filename=' filename=STRING '/>';

TMTCIFFieldValueEnum returns fieldvalue::TMTCIFFieldValueEnum:
	'<FieldValueEnum' 'enumRef=' enumRef=[enum_::TMTCIFEnum|STRING] 'valueRef=' valueRef=[enum_::TMTCIFEnumValue|STRING] '/>';

terminal UINT_STRING returns ecore::EString:
	('"' ('0'..'9')+ '"');

terminal HEX_STRING returns ecore::EString:
	('"0x' (('0'..'9') | ('a'..'f') | ('A'..'F'))+ '"');