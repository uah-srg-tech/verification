module es.uah.aut.srg.micobs.svm.lang.srs.GenerateSRS

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "es.uah.aut.srg.micobs.svm.editor.srs"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = 
				"/*******************************************************************************
 * Copyright (c) 2018 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     SRG Team - Initial API and implementation
 ******************************************************************************/"
			}
		}
		language = StandardLanguage {
			name = "es.uah.aut.srg.micobs.svm.lang.srs.SRS"
			fileExtensions = "svm_srs"
			referencedResource = "platform:/resource/es.uah.aut.srg.micobs.svm/model/srs.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

    		// Update the options for the Antlr Parser Generator
		    parserGenerator = {
		        options = {
		            backtrack = true
		            classSplitting = true
		        }
		    }
		
		    // Add ImportURIScopingFragment and SimpleNamesFragment
		    fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
		        fragment = org.eclipse.xtext.generator.ecore2xtext.FormatterFragment {}
		        fragment = org.eclipse.xtext.generator.scoping.ImportURIScopingFragment {}
		        fragment = org.eclipse.xtext.generator.exporting.SimpleNamesFragment {}
		    }
		    
		    // Validator for the ImportURIScopingFragment
		    validator = {
		        composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }
		
		    // Scope provider options
		    scopeProvider = {
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }           
		    
		    // Disable the generation of the code generator
		    generator = {
		        generateStub = false
		    }
		
		    // No serializer
		    serializer = {
		        generateStub = false
		    }
		    
		    // Content assist options
		    contentAssist = {
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }
		    
		    // Label provider options
		    labelProvider = {
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }
		    
		    // Outline provider options
		    outline = {
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }
		    
		    // Quick-fix provider options
		    quickFixProvider = {
		        generateXtendStub = false // No Xtend for me, please, just Java
		    }
		    
		    // General classes
		    generateXtendStubs = false // No Xtend for me, please, just Java
		}
	}
}